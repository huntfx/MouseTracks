; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Command: ISCC.exe /DMyAppVersion="2.0.0" /DMySourceBaseName="dist\MouseTracks-2.0.0-windows-x64" /DMyDestinationBaseName="dist\MouseTracks-2.0.0-windows-x64-setup" MouseTracks.iss

#define MyAppName "MouseTracks"

; You can either hardcode the version here or pass it in via command line
; using /DMyAppVersion="2.0.0" when compiling.
#ifndef MyAppVersion
  #define MyAppVersion "2.0.0"
#endif

; Define the path to the source executable
; Example: MouseTracks-2.0.0-windows-x64
#ifndef MySourceBaseName
  #define MySourceBaseName "dist\MouseTracks"
#endif

; Define the full path to the output installer
#ifndef MyDestinationBaseName
  #define MyDestinationBaseName "dist\MouseTracksSetup"
#endif

#define MyAppPublisher "Peter Hunt"
#define MyAppURL "https://github.com/huntfx/MouseTracks"

[Setup]
AppId={{4FABD8FD-8B74-42D0-A4B6-A8F96BDCBBAD}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}/issues
AppUpdatesURL={#MyAppURL}/releases

UninstallDisplayName={#MyAppName}
UninstallDisplayIcon={app}\icon.ico

VersionInfoVersion={#MyAppVersion}
VersionInfoProductVersion={#MyAppVersion}
VersionInfoDescription={#MyAppName} {#MyAppVersion} Setup
VersionInfoProductName={#MyAppName}
VersionInfoCopyright=Peter Hunt

DefaultDirName={localappdata}\{#MyAppName}
; Support appdata installs only
PrivilegesRequired=lowest
; Create a "MouseTracks" folder in the Start Menu
DefaultGroupName={#MyAppName}

DisableDirPage=yes
DisableProgramGroupPage=yes
DisableWelcomePage=no

Compression=lzma2
SolidCompression=yes

; Extract the directory and filename from MyDestinationBaseName
OutputDir={#ExtractFilePath(MyDestinationBaseName)}
OutputBaseFilename={#ExtractFileName(MyDestinationBaseName)}

SetupIconFile=resources\images\icon.ico
LicenseFile=LICENSE.md

ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible

; Tells the installer to check if the app is running
CloseApplications=yes
RestartApplications=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "autostart"; Description: "Launch {#MyAppName} automatically when Windows starts"; GroupDescription: "Startup options:"
Name: "startminimised"; Description: "Minimise to the system tray on startup"; GroupDescription: "Startup options:"

[Registry]
; Write to the standard Windows "Run" key for the current user
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "{#MyAppName}"; ValueData: """{app}\{#MyAppName}.exe"" --installed --autostart{code:GetStartupArgs}"; Flags: uninsdeletevalue; Tasks: autostart

[Files]
Source: "dist\{#MyAppName}.exe"; DestDir: "{app}"; DestName: "{#MyAppName}.exe"; Flags: ignoreversion
Source: "{#MySourceBaseName}.exe"; DestDir: "{app}"; Flags: ignoreversion

Source: "README.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "LICENSE.md"; DestDir: "{app}"; Flags: ignoreversion

; Source: "config\*"; DestDir: "{app}\config"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "resources\images\icon.ico"; DestDir: "{app}"; DestName: "icon.ico"; Flags: ignoreversion

[Icons]
; Start Menu shortcut
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppName}.exe"
; Desktop shortcut
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppName}.exe"; Tasks: desktopicon

[Run]
; Launch the app after installation is complete
Filename: "{app}\{#MyAppName}.exe"; Parameters: "{code:GetStartupArgs}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
// Function to dynamically determine startup arguments based on selected tasks
function GetStartupArgs(Param: String): String;
begin
  // Check if the "Start minimised" task is checked
  if WizardIsTaskSelected('startminimised') then
  begin
    Result := ' --start-hidden';
  end
  else
  begin
    Result := ' --start-visible';
  end;
end;

// Function to check if the application is currently running
function InitializeSetup(): Boolean;
var
  WbemLocator, WbemServices, WbemObjectSet: Variant;
  TargetFile: String;
begin
  Result := True;

  TargetFile := ExpandConstant('{localappdata}\{#MyAppName}\{#MyAppName}.exe');
  StringChange(TargetFile, '\', '\\');

  try
    WbemLocator := CreateOleObject('WbemScripting.SWbemLocator');
    WbemServices := WbemLocator.ConnectServer('.', 'root\CIMV2');

    WbemObjectSet := WbemServices.ExecQuery('SELECT Name FROM Win32_Process WHERE ExecutablePath = ''' + TargetFile + '''');

    if not VarIsNull(WbemObjectSet) and (WbemObjectSet.Count > 0) then
    begin
      MsgBox('MouseTracks is currently running. Please close it before installing.', mbError, MB_OK);
      Result := False;
    end;

  except
    // If WMI fails, proceed anyway
  end;
end;
